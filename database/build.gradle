plugins {
    id 'java-library'
    id "org.flywaydb.flyway" version '5.2.4'
    id 'nu.studer.jooq' version '3.0.3'
}

configurations {
    flywayMigration
}

sourceCompatibility = 11
targetCompatibility = 11


dependencies {

    //jooq
    implementation 'org.jooq:jooq'
    jooqRuntime('org.jooq:jooq-meta:3.11.9')
    jooqRuntime('org.jooq:jooq-codegen:3.11.9')

    //jaxb
    //compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation('javax.annotation:javax.annotation-api:1.3.2')

    //flyway
    api "org.flywaydb:flyway-core:5.2.4"

    //postgres
    jooqRuntime("org.postgresql:postgresql:42.2.8")
    flywayMigration("org.postgresql:postgresql:42.2.8")

}

jooq {
    version = jooqVersion
    edition = 'OSS'

    delivery(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = project.property('db.local.jdbcUrl')
            user = project.property('db.local.user')
            password = project.property('db.local.password')
            schema = 'public'
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = false
                fluentSetters = true
                javaTimeTypes=true
            }
            target {
                packageName = 'com.learnjooq.generated'
                directory = 'src/main/java'
            }
        }
    }
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/jooqtest'
    user = 'postgres'
    password = 'postgres'
    schemas = ['public']
    configurations = [ 'compile', 'flywayMigration' ]
}

project.tasks.getByName('generateDeliveryJooqSchemaSource').dependsOn flywayMigrate

project.tasks.getByName('generateDeliveryJooqSchemaSource').inputs.dir("src/main/resources/db/migration")

